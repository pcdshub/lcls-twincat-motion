<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.14">
  <POU Name="FB_AxisHardware" Id="{1564163d-e493-4d9a-b680-85c98f09b3c4}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_AxisHardware
VAR_INPUT
	bExecute: BOOL;
	amsEncAddr: AMSADDR;
	nEncCh: USINT;
END_VAR
VAR_IN_OUT
	Axis: AXIS_REF;
END_VAR
VAR_OUTPUT
	bDone: BOOL;
	bBusy: BOOL;
	bError: BOOL;
	nErrorID: UDINT;
	stHardware: ST_AxisHardware;
END_VAR
VAR
	fbReadDriveAddress: MC_ReadDriveAddress;
	fbDriveId: FB_EcGetSlaveIdentity;
	fbEncId: FB_EcGetSlaveIdentity;
	fbADSRead: ADSREADEX;
	stDriveAddr: ST_DriveAddress;
	stDriveRef: ST_DriveRef;
	stEncRef: ST_DriveRef;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Get drive ref using AXIS_REF
fbReadDriveAddress(Execute:=bExecute,
	Axis:=Axis,
	DriveAddress=>stDriveAddr);
stDriveRef.sNetId := stDriveAddr.NetID;
stDriveRef.nSlaveAddr := stDriveAddr.SlaveAddress;
stDriveRef.nDriveNo := stDriveAddr.Channel;
stHardware.drive_ref := stDriveRef;

// Get encoder ref using input args (cannot be derived from AXIS_REF)
stEncRef.sNetId := F_CreateAmsNetId(amsEncAddr.netId);
stEncRef.nSlaveAddr := amsEncAddr.port;
stEncRef.nDriveNo := nEncCh;
stHardware.enc_ref := stEncRef;

// Get the drive model (e.g. 7031)
IF stDriveRef.nSlaveAddr > 0 THEN
	fbDriveId(bExecute:=bExecute AND fbReadDriveAddress.Done,
		sNetId:=stDriveRef.sNetId,
		nSlaveAddr:=stDriveRef.nSlaveAddr);
	stHardware.drive_type := UDINT_TO_UINT(SHR(fbDriveID.identity.productCode, 16));
END_IF

// Get the encoder model (e.g. 5042)
IF stEncRef.nSlaveAddr > 0 THEN
	fbEncId(bExecute:=bExecute AND fbReadDriveAddress.Done,
		sNetId:=stEncRef.sNetId,
		nSlaveAddr:=stEncRef.nSlaveAddr);
	stHardware.enc_type := UDINT_TO_UINT(SHR(fbEncID.identity.productCode, 16));
END_IF

// Summarize outputs
bDone := fbReadDriveAddress.Done AND NOT fbDriveId.bBusy AND NOT fbEncId.bBusy;
bBusy := fbReadDriveAddress.Busy OR fbDriveId.bBusy OR fbEncId.bBusy;
bError := fbReadDriveAddress.Error OR fbDriveId.bError OR fbEncId.bError;
IF fbReadDriveAddress.ErrorID > 0 THEN
	nErrorID := fbReadDriveAddress.ErrorID;
ELSIF fbDriveId.nErrId >0 THEN
	nErrorID := fbDriveId.nErrId;
ELSE
	nErrorID := fbEncId.nErrId;
END_IF]]></ST>
    </Implementation>
    <LineIds Name="FB_AxisHardware">
      <LineId Id="18" Count="1" />
      <LineId Id="38" Count="1" />
      <LineId Id="44" Count="2" />
      <LineId Id="79" Count="0" />
      <LineId Id="83" Count="0" />
      <LineId Id="85" Count="0" />
      <LineId Id="90" Count="2" />
      <LineId Id="84" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="100" Count="0" />
      <LineId Id="20" Count="2" />
      <LineId Id="27" Count="0" />
      <LineId Id="81" Count="0" />
      <LineId Id="101" Count="0" />
      <LineId Id="86" Count="0" />
      <LineId Id="102" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="95" Count="2" />
      <LineId Id="94" Count="0" />
      <LineId Id="103" Count="0" />
      <LineId Id="82" Count="0" />
      <LineId Id="47" Count="4" />
      <LineId Id="53" Count="1" />
      <LineId Id="98" Count="1" />
      <LineId Id="52" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>