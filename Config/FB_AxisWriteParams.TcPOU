<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.14">
  <POU Name="FB_AxisWriteParams" Id="{c12a06b7-df78-4119-aa24-9aeca1f8090f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_AxisWriteParams
VAR_INPUT
	bExecute: BOOL;
	bReset: BOOL;
	stConfig: ST_AxisParams;
	stHardware: ST_AxisHardware;
END_VAR
VAR_IN_OUT
	Axis: AXIS_REF;
END_VAR
VAR_OUTPUT
	bInitPreOp: BOOL;
	bDone: BOOL;
	bBusy: BOOL;
	bError: BOOL;
	nErrorID: UDINT;
END_VAR
VAR
	bDoWrite: BOOL;
	fbNCWrite: FB_AxisWriteNC;
	fbCoEWrite: FB_AxisWriteCoE;
	fbPrepWrite: FB_AxisSetSlaveState;
	fbInitState: FB_AxisSetSlaveState;
	fbPreOpState: FB_AxisSetSlaveState;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Do not write unless axis is disabled. Leave TRUE after done to avoid resetting errors, etc.
bDoWrite := bExecute AND (Axis.Status.MotionState = MC_AXISSTATE_DISABLED OR bDone);
// Move to PREOP to prepare for writing
fbPrepWrite(bExecute:=bDoWrite,
	stHardware:=stHardware,
	reqState:=EC_DEVICE_STATE_PREOP);
// The first time we're here, report that we've set to PREOP so master can move to OP
IF fbPrepWrite.bDone AND NOT fbPrepWrite.bError THEN
	bInitPreOP := TRUE;
END_IF
// Do the write
fbNCWrite(bExecute:=fbPrepWrite.bDone AND NOT fbPrepWrite.bError,
	bReset:=bReset,
	stConfig:=stConfig,
	Axis:=Axis);
fbCoEWrite(bExecute:=fbNCWrite.bExecute,
	bReset:=bReset,
	stHardware:=stHardware,
	stConfig:=stConfig,
	Axis:=Axis);
// Move to INIT and back to PRE-OP for params that need a full restart
fbInitState(bExecute:=fbNCWrite.bDone AND NOT fbNCWrite.bError AND NOT fbCoEWrite.bBusy AND NOT fbCoEWrite.bError,
	stHardware:=stHardware,
	reqState:=EC_DEVICE_STATE_INIT);
fbPreOpState(bExecute:=fbInitState.bDone AND NOT fbInitState.bError,
	stHardware:=stHardware,
	reqSTate:=EC_DEVICE_STATE_PREOP);
bDone := fbPreOpState.bDone;
bBusy := fbNCWrite.bBusy OR fbCoEWrite.bBusy OR fbPrepWrite.bBusy OR fbInitState.bBusy OR fbPreOpState.bBusy;
bError := fbNCWrite.bError OR fbCoEWrite.bError OR fbPrepWrite.bError OR fbInitState.bError OR fbPreOpState.bError;
IF fbNCWrite.nErrorID > 0 THEN
	nErrorID := fbNCWrite.nErrorID;
ELSIF fbCoeWrite.nErrorID > 0 THEN
	nErrorID := fbCoEWrite.nErrorID;
ELSIF fbPrepWrite.nErrId > 0 THEN
	nErrorID := fbPrepWrite.nErrId;
ELSIF fbInitSTate.nErrId > 0 THEN
	nErrorID := fbInitState.nErrId;
ELSE
	nErrorID := fbPreOpState.nErrId;
END_IF]]></ST>
    </Implementation>
    <LineIds Name="FB_AxisWriteParams">
      <LineId Id="47" Count="0" />
      <LineId Id="165" Count="1" />
      <LineId Id="146" Count="1" />
      <LineId Id="167" Count="0" />
      <LineId Id="151" Count="0" />
      <LineId Id="169" Count="2" />
      <LineId Id="38" Count="0" />
      <LineId Id="29" Count="1" />
      <LineId Id="32" Count="0" />
      <LineId Id="82" Count="4" />
      <LineId Id="123" Count="0" />
      <LineId Id="172" Count="5" />
      <LineId Id="89" Count="4" />
      <LineId Id="95" Count="1" />
      <LineId Id="179" Count="5" />
      <LineId Id="94" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>