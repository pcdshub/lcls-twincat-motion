<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.14">
  <POU Name="FB_AxisWriteCoE" Id="{8322add8-d459-4485-a3b6-233edce37cb6}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_AxisWriteCoE
VAR_INPUT
	bExecute: BOOL;
	bReset: BOOL;
	stHardware: ST_AxisHardware;
END_VAR
VAR_IN_OUT
	stConfig: ST_AxisParams;
	Axis: AXIS_REF;
END_VAR
VAR_OUTPUT
	bDone: BOOL;
	bBusy: BOOL;
	bError: BOOL;
	nErrorID: UDINT;
END_VAR
VAR
	fbWrite70x1: FB_AxisWrite70x1;
	fbWrite5042: FB_AxisWrite5042;
	bBusyDrive: BOOL;
	bBusyEnc: BOOL;
	bErrorDrive: BOOL;
	bErrorEnc: BOOL;
	iErrorDriveADS: UDINT;
	iErrorEncADS: UDINT;
	iErrorDriveCAN: UDINT;
	iErrorEncCAN: UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[CASE stHardware.drive_type OF
	0:
		bBusyDrive := FALSE;
		bErrorDrive := FALSE;
		iErrorDriveAds := 0;
		iErrorDriveCAN := 0;
	7031, 7041:
		fbWrite70x1(bExecute:=bExecute,
			bReset:=bReset,
			stDriveRef:=stHardware.drive_ref,
			stConfig:=stConfig,
			Axis:=Axis,
			bBusy=>bBusyDrive,
			bError=>bErrorDrive,
			iAdsErrId=>iErrorDriveAds,
			iCANopenErrId=>iErrorDriveCAN);
	ELSE
		bBusyDrive := FALSE;
		bErrorDrive := TRUE;
		iErrorDriveAds := 1;
		iErrorDriveCAN := 0;
END_CASE
CASE stHardware.enc_type OF
	0:
		bBusyEnc := FALSE;
		bErrorEnc := FALSE;
		iErrorEncAds := 0;
		iErrorEncCAN := 0;
	5042:
		fbWrite5042(bExecute:=bExecute,
			bReset:=bReset,
			stDriveRef:=stHardware.enc_ref,
			stConfig:=stConfig,
			Axis:=Axis,
			bBusy=>bBusyEnc,
			bError=>bErrorEnc,
			iAdsErrId=>iErrorEncAds,
			iCANopenErrId=>iErrorEncCAN);
	ELSE
		bBusyEnc := FALSE;
		bErrorEnc := TRUE;
		iErrorEncAds := 1;
		iErrorEncCAN := 0;
END_CASE
bBusy := bBusyDrive OR bBusyEnc;
bDone := bExecute AND NOT bBusy;
bError := bErrorDrive OR bErrorEnc;
IF iErrorDriveAds > 0 THEN
	nErrorID := iErrorDriveAds;
ELSIF iErrorDriveCAN > 0 THEN
	nErrorID := iErrorDriveCAN;
ELSIF iErrorEncAds > 0 THEN
	nErrorID := iErrorEncAds;
ELSE
	nErrorID := iErrorEncCAN;
END_IF
]]></ST>
    </Implementation>
    <LineIds Name="FB_AxisWriteCoE">
      <LineId Id="43" Count="43" />
      <LineId Id="126" Count="0" />
      <LineId Id="87" Count="10" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>