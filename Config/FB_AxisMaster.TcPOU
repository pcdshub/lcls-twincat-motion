<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.14">
  <POU Name="FB_AxisMaster" Id="{ffde42e9-8ab9-4b2d-b01e-b36e19e1042f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_AxisMaster
VAR_INPUT
	bInit: BOOL := TRUE;
	bEnable: BOOL;
	stConfigSet: ST_AxisParams;
	bMPSEnableFwd: BOOL := TRUE;
	bMPSEnableBwd: BOOL := TRUE;
	bEPSEnableFwd: BOOL := TRUE;
	bEPSEnableBwd: BOOL := TRUE;
	bAUXEnableFwd: BOOL := TRUE;
	bAUXEnableBwd: BOOL := TRUE;
	bLimitSwitch: BOOL := TRUE;
	bInitCfg: BOOL := TRUE;
	bCfgRO: BOOL := FALSE;
	nEncCh: USINT;
	nDelayInit: USINT := 10;
END_VAR
VAR_OUTPUT
	fPosition: LREAL;
	stAxisStatus: ST_AxisStatus;
	stConfigGet: ST_AxisParams;
	bInitPreOp: BOOL;
END_VAR
VAR
	bBrakeOut AT %Q*: BOOL;
	bLimitFwd AT %I*: BOOL;
	bLimitBwd AT %I*: BOOL;
	bHomeSensor AT %I*: BOOL;
	amsEncAddr AT %I*: AMSADDR;
	fbLink: FB_NcAxis;
	fbHardware: FB_AxisHardware;
	fbConfig: FB_AxisConfig;
	fbMasterGetOP: FB_EcGetMasterState;
	fbAxisSetOP: FB_AxisSetSlaveState;
	fbDrive: FB_DriveVirtual;
	bDisableAndExecConfig: BOOL;
	nDelayCycles: USINT := 0;
	bOpReady: BOOL;
	bAltInitDone: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Landing zone to link the AXIS_REF and update it
fbLink(En:=bInit);
// Before proceeding, delay cycles so we can fully communicate with terminals
IF nDelayCycles < nDelayInit THEN
	nDelayCycles := nDelayCycles + 1;
END_IF
// Get terminal info
fbHardware(bExecute:=bInit AND nDelayCycles >= nDelayInit,
	Axis:=fbLink.Axis,
	amsEncAddr:=amsEncAddr,
	nEncCh:=nEncCh);
// If asked to initialize and we haven't, then initialize
IF bInitCfg AND NOT fbConfig.bInitDone THEN
	bDisableAndExecConfig := TRUE;
// If read-only and not initializing, reject the write command
ELSIF bCfgRO THEN
	bDisableAndExecConfig := FALSE;
// Otherwise, keep the value written over ADS
END_IF
// Read and/or write the config
fbConfig(bExecute:=bDisableAndExecConfig AND fbHardware.bDone,
	bExecGet:=NOT bInitCfg AND NOT bDisableAndExecConfig AND fbHardware.bDone AND NOT fbConfig.bInitDone,
	stConfigSet:=stConfigSet,
	stHardware:=fbHardware.stHardware,
	Axis:=fbLink.Axis,
	bInitPreOp=>bInitPreOp,
	stConfigGet=>stConfigGet);
// Reset the execute bool
IF fbConfig.bDone AND NOT fbConfig.bError THEN
	bDisableAndExecConfig := FALSE;
	fbConfig.bReset := TRUE;
END_IF
// Adjust the set config to match the get config if we didn't ask for bInitCfg
IF NOT bInitCfg AND fbConfig.bInitDone AND NOT bAltInitDone THEN
	stConfigSet := stConfigGet;
	bAltInitDone := TRUE;
END_IF
// After the initial config is done, go to OP mode
// But first, make sure the master is in OP mode
// This block repeately toggles the fbMasterGetOP read off/on until we are OP
IF fbHardware.stHardware.drive_type > 0 THEN
	IF fbConfig.bInitDone AND NOT fbConfig.bError AND fbMasterGetOP.state <> EC_DEVICE_STATE_OP THEN
		IF fbMasterGetOP.bExecute AND NOT fbMasterGetOP.bBusy THEN
			fbMasterGetOP.bExecute := FALSE;
		ELSE
			fbMasterGetOP.bExecute := TRUE;
		END_IF
	END_IF
	fbMasterGetOP(sNetId:=fbHardware.stHardware.drive_ref.sNetId);
	fbAxisSetOP(bExecute:=fbMasterGetOP.state = EC_DEVICE_STATE_OP AND NOT fbMasterGetOP.bBusy AND NOT fbMasterGetOP.bError,
		stHardware:=fbHardware.stHardware,
		reqState:=EC_DEVICE_STATE_OP);
	IF fbAxisSetOp.bDone AND NOT fbAxisSetOp.bError THEN
		bOpReady := TRUE;
	END_IF
ELSIF fbConfig.bInitDone THEN
	bOpReady := TRUE;
END_IF
// Disable brake while executing move
bBrakeOut := fbLink.Status.HasJob;
IF NOT bLimitSwitch THEN
	bLimitFwd := TRUE;
	bLimitBwd := TRUE;
END_IF
// Execute incoming motion commands
fbDrive(En:=bEnable,
	bEnable:=bEnable AND NOT bDisableAndExecConfig AND bOpReady,
	bLimitFwd:=bLimitFwd AND bMPSEnableFwd AND bEPSEnableFwd AND bAUXEnableFwd,
	bLimitBwd:=bLimitBwd AND bMPSEnableBwd AND bEPSEnableBwd AND bAUXEnableBwd,
	bHomeSensor:=bHomeSensor,
	Axis:=fbLink.Axis,
	Status=>stAxisStatus,
	fActPosition=>fPosition);
// Reset the execute bool
IF fbDrive.bDone AND NOT fbDrive.bError THEN
	fbDrive.bExecute := FALSE;
END_IF]]></ST>
    </Implementation>
    <LineIds Name="FB_AxisMaster">
      <LineId Id="155" Count="0" />
      <LineId Id="584" Count="1" />
      <LineId Id="592" Count="0" />
      <LineId Id="591" Count="0" />
      <LineId Id="214" Count="0" />
      <LineId Id="203" Count="0" />
      <LineId Id="212" Count="0" />
      <LineId Id="439" Count="1" />
      <LineId Id="484" Count="2" />
      <LineId Id="490" Count="0" />
      <LineId Id="488" Count="1" />
      <LineId Id="491" Count="0" />
      <LineId Id="487" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="595" Count="0" />
      <LineId Id="119" Count="0" />
      <LineId Id="249" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="534" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="441" Count="2" />
      <LineId Id="653" Count="0" />
      <LineId Id="444" Count="0" />
      <LineId Id="655" Count="2" />
      <LineId Id="661" Count="0" />
      <LineId Id="658" Count="0" />
      <LineId Id="151" Count="0" />
      <LineId Id="542" Count="0" />
      <LineId Id="594" Count="0" />
      <LineId Id="596" Count="0" />
      <LineId Id="546" Count="1" />
      <LineId Id="550" Count="0" />
      <LineId Id="552" Count="1" />
      <LineId Id="551" Count="0" />
      <LineId Id="548" Count="0" />
      <LineId Id="543" Count="0" />
      <LineId Id="446" Count="0" />
      <LineId Id="540" Count="1" />
      <LineId Id="601" Count="2" />
      <LineId Id="599" Count="1" />
      <LineId Id="597" Count="0" />
      <LineId Id="536" Count="0" />
      <LineId Id="481" Count="0" />
      <LineId Id="524" Count="1" />
      <LineId Id="527" Count="0" />
      <LineId Id="526" Count="0" />
      <LineId Id="535" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="47" Count="0" />
      <LineId Id="451" Count="2" />
      <LineId Id="65" Count="0" />
      <LineId Id="650" Count="0" />
      <LineId Id="648" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="158" Count="0" />
      <LineId Id="18" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>