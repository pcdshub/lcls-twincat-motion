<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_MotionStage" Id="{137c52b5-8983-4728-b719-31601503f20a}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_MotionStage
(*
    Default implementation for PLC behavior when motor IOC asks for a move
    This can be extended or replaced in your PLC project if you want
    non-default behavior to arise from the motor record processing
*)
VAR_IN_OUT
    stMotionStage: ST_MotionStage;
END_VAR
VAR
    fbDriveVirtual: FB_DriveVirtual;
    fbMotionHome: FB_MotionHoming;
    fbSaveRestore: FB_EncSaveRestore;
    fbLogError: FB_LogMotionError;
	fbBacklashCompensation: FB_MotionBacklashCompensation;
    bExecute: BOOL;
    bExecMove: BOOL;
    bExecHome: BOOL;
    bFwdHit: BOOL;
    bBwdHit: BOOL;
    ftExec: F_TRIG;
    rtExec: R_TRIG;
    rtUserExec: R_TRIG;
    rtTarget: R_TRIG;
    rtHomed: R_TRIG;
    fbSetEnables: FB_SetEnables;
    bPosGoal: BOOL;
    bNegGoal: BOOL;
    fbEncoderValue: FB_EncoderValue;
    fbNCParams: FB_MotionStageNCParams;
    bNewMoveReq: BOOL;
    bPrepareDisable: BOOL;
    bMoveCmd: BOOL;
    rtMoveCmdShortcut: R_TRIG;
    rtHomeCmdShortcut: R_TRIG;
	// backlash compensation
	bBacklashEnabled: BOOL;
    bBacklashBusy: BOOL;
    bBacklashError: BOOL;
    nBacklashErrorID: UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Start with an accurate status
stMotionStage.Axis.ReadStatus();

// Check for the plc shortcut commands
// Used for testing or to circumvent motor record issues
rtMoveCmdShortcut(CLK:=stMotionStage.bMoveCmd);
rtHomeCmdShortcut(CLK:=stMotionStage.bHomeCmd);
// Execute on rising edge
IF rtMoveCmdShortcut.Q AND NOT stMotionStage.bExecute THEN
    stMotionStage.bExecute := TRUE;
    stMotionStage.nCommand := E_EpicsMotorCmd.MOVE_ABSOLUTE;
ELSIF rtHomeCmdShortcut.Q AND NOT stMotionStage.bExecute THEN
    stMotionStage.bExecute := TRUE;
    stMotionStage.nCommand := E_EpicsMotorCmd.HOME;
	// maybe disable backlash here
END_IF
// Always reset, even if not rising edge, so command can be issued again
IF stMotionStage.bMoveCmd OR stMotionStage.bHomeCmd THEN
    stMotionStage.bMoveCmd := FALSE;
    stMotionStage.bHomeCmd := FALSE;
END_IF

// Automatically fill the correct nCmdData for homing
IF stMotionStage.nCommand = E_EpicsMotorCmd.HOME THEN
    stMotionStage.nCmdData := stMotionStage.nHomingMode;
	// or maybe here
END_IF

// Check if the command wants to cause a move
bMoveCmd R= stMotionStage.nCmdData = E_EpicsHomeCmd.ABSOLUTE_SET;
bMoveCmd R= stMotionStage.nCmdData = E_EpicsHomeCmd.NONE;
bMoveCmd S= stMotionStage.nCommand <> E_EpicsMotorCmd.HOME;

// Handle main execs
rtUserExec(CLK := stMotionStage.bExecute);
bNewMoveReq S= rtUserExec.Q AND bMoveCmd;
bNewMoveReq R= NOT stMotionStage.bExecute;
bPrepareDisable R= bNewMoveReq;

bPosGoal := stMotionStage.stAxisStatus.fActPosition < stMotionStage.fPosition;
bNegGoal := stMotionStage.stAxisStatus.fActPosition > stMotionStage.fPosition;

// Moves are automatically allowed if no safety hooks. Otherwise, some other code will set this.
stMotionStage.bSafetyReady S= stMotionStage.bPowerSelf;

// Handle auto-enable timing
CASE stMotionStage.nEnableMode OF
    E_StageEnableMode.ALWAYS:
        stMotionStage.bEnable := TRUE;
    E_StageEnableMode.DURING_MOTION:
        IF bNewMoveReq THEN
            IF stMotionStage.nCommand = E_EpicsMotorCmd.HOME THEN
                stMotionStage.bEnable := stMotionStage.bSafetyReady;
            ELSIF bPosGoal THEN
                IF stMotionStage.bAllForwardEnable THEN
                    stMotionStage.bEnable S= stMotionStage.bSafetyReady;
                ELSIF NOT stMotionStage.bError THEN
                    // Not an error, just a warning
                    stMotionStage.sErrorMessage := 'Cannot move past positive limit.';
                    stMotionStage.bExecute := FALSE;
                END_IF
            ELSIF bNegGoal THEN
                IF stMotionStage.bAllBackwardEnable THEN
                    stMotionStage.bEnable S= stMotionStage.bSafetyReady;
                ELSIF NOT stMotionStage.bError THEN
                    // Not an error, just a warning
                    stMotionStage.sErrorMessage := 'Cannot move past negative limit.';
                    stMotionStage.bExecute := FALSE;
                END_IF
            ELSE
                // Super rare condition where we asked for a move to exactly the same floating point we're already at
                stMotionStage.bEnable S= stMotionStage.bSafetyReady;
            END_IF
            IF stMotionStage.bEnable OR stMotionStage.bError THEN
                bNewMoveReq := FALSE;
            END_IF
        END_IF
END_CASE

// Update all enable booleans
fbSetEnables(stMotionStage:=stMotionStage);

IF stMotionStage.stAxisStatus.bBusy AND NOT bExecute THEN
    // Wait for the previous move to end
    bExecute := FALSE;
ELSIF bMoveCmd THEN
    // Do not start the move until we have power and the safety system says it is OK
    bExecute := stMotionStage.bExecute AND stMotionStage.bAllEnable AND stMotionStage.bEnableDone AND stMotionStage.bSafetyReady;
ELSE
    bExecute := stMotionStage.bExecute;
END_IF

IF bExecute AND NOT stMotionStage.bError THEN
    // Reset local warnings if things are going well
    stMotionStage.sErrorMessage := '';
END_IF

// No moves allowed in error states
IF stMotionStage.bError THEN
    bExecute := FALSE;
END_IF

bExecHome := bExecute AND (stMotionStage.nCommand = 10);
// absolute move is valid only after after backlash compensation is enabled
bExecMove := bExecute AND NOT bExecHome AND bBacklashEnabled; 

// backlash compensention handling
fbBacklashCompensation(stMotionStage:=stMotionStage, 
	bHoming:=bExecHome, 
	bMove:=bExecMove, 
	bEnabled=>bBacklashEnabled);
	
// Handle standard commands using ESS's FB
fbDriveVirtual(En:=TRUE,
    bEnable:=stMotionStage.bAllEnable,
    bReset:=stMotionStage.bReset,
    bExecute:=bExecMove, // add a condition on this boolean, backlsash needs to be enabled
    nCommand:=INT_TO_UINT(stMotionStage.nCommand),
    nCmdData:=INT_TO_UINT(stMotionStage.nCmdData),
    fVelocity:=stMotionStage.fVelocity,
    fPosition:=stMotionStage.fPosition,
    fAcceleration:=stMotionStage.fAcceleration,
    fDeceleration:=stMotionStage.fDeceleration,
    bLimitFwd:=stMotionStage.bAllForwardEnable,
    bLimitBwd:=stMotionStage.bAllBackwardEnable,
    bHomeSensor:=stMotionStage.bHome,
    fHomePosition:=stMotionStage.fHomePosition,
    bPowerSelf:=stMotionStage.bPowerSelf,
    nMotionAxisID=>stMotionStage.nMotionAxisID,
    Axis:=stMotionStage.Axis);

// Some custom home handling
fbMotionHome(
    stMotionStage:=stMotionStage,
    bExecute:=bExecHome);

// Update status again after the move starts or stops
stMotionStage.Axis.ReadStatus();

// Check for a new error
IF NOT stMotionStage.bError THEN
    stMotionStage.bError := fbDriveVirtual.bError;
    stMotionStage.nErrorId := fbDriveVirtual.nErrorId;
END_IF

IF NOT stMotionStage.bError THEN
    stMotionStage.bError := fbBacklashCompensation.bError;
    stMotionStage.nErrorId := fbBacklashCompensation.nErrorId;
END_IF

IF NOT stMotionStage.bError THEN
    stMotionStage.bError := fbMotionHome.bError;
    stMotionStage.nErrorId := fbMotionHome.nErrorId;
END_IF
IF NOT stMotionStage.bError AND stMotionStage.bExecute AND NOT stMotionStage.bUserEnable THEN
    stMotionStage.bError := TRUE;
    stMotionStage.nErrorId := 1;
    stMotionStage.sCustomErrorMessage := 'Move requested, but user enable is disabled!';
END_IF

// Set the error message if we have one
IF stMotionStage.bError THEN
    // Hook if other code wants to inject a non-NC error
    IF stMotionStage.sCustomErrorMessage <> '' THEN
        stMotionStage.sErrorMessage := stMotionSTage.sCustomErrorMessage;
    ELSE
        stMotionStage.sErrorMessage := F_MotionErrorCodeLookup(nErrorId := stMotionStage.nErrorId);
    END_IF
END_IF

fbLogError(
    stMotionStage:=stMotionStage,
    bEnable:=TRUE);

// When we start, set the busy/done appropriately
rtExec(CLK:=bExecute);
IF rtExec.Q THEN
    stMotionStage.bBusy := TRUE;
    stMotionStage.bDone := FALSE;
END_IF

// Force everything off in case of error
IF stMotionStage.bError THEN
    stMotionStage.bBusy := FALSE;
    stMotionStage.bDone := FALSE;
    stMotionStage.bEnable := FALSE;
END_IF

// Check the limits and cancel execution if appropriate. Without this block we have infinite error spam
bFwdHit := stMotionStage.Axis.Status.PositiveDirection AND NOT stMotionStage.bAllForwardEnable;
bBwdHit := stMotionStage.Axis.Status.NegativeDirection AND NOT stMotionStage.bAllBackwardEnable;
IF (bFwdHit OR bBwdHit) AND NOT fbMotionHome.bBusy THEN
    stMotionStage.bExecute := FALSE;
END_IF

// Check done moving via user stop, fbDriveVirtual and Target Position Monitoring, or from homing.
ftExec(CLK:=stMotionStage.bExecute);
rtTarget(CLK:=(stMotionStage.Axis.Status.InTargetPosition AND fbDriveVirtual.bDone AND bExecMove));
rtHomed(CLK:=fbMotionHome.bDone AND bExecHome);
IF ftExec.Q OR rtTarget.Q OR rtHomed.Q THEN
    IF NOT stMotionStage.bDone THEN
        stMotionStage.bDone := TRUE;
        stMotionStage.bBusy := FALSE;
        IF NOT stMotionStage.Axis.Status.Error THEN
            bExecute := FALSE;
            stMotionStage.bExecute := FALSE;
        END_IF
    END_IF
END_IF

// Handle auto-disable timing
bPrepareDisable S= stMotionStage.nEnableMode = E_StageEnableMode.DURING_MOTION AND ftExec.Q;
// Delay the disable until we reach standstill, else brake issues or other race conditions
IF bPrepareDisable AND stMotionStage.Axis.Status.MotionState = MC_AXISSTATE_STANDSTILL THEN
    bPrepareDisable := FALSE;
    stMotionStage.bEnable := FALSE;
END_IF

// Get a definitive bEnabled reading
CASE stMotionStage.Axis.Status.MotionState OF
    // We are not enabled if there is an issue
    MC_AXISSTATE_UNDEFINED, MC_AXISSTATE_DISABLED, MC_AXISSTATE_ERRORSTOP:
        stMotionStage.bEnableDone := FALSE;
    ELSE
        stMotionStage.bEnableDone := TRUE;
END_CASE

// Handle the brake. TRUE means brake disabled/released
IF stMotionStage.nBrakeMode <> E_StageBrakeMode.NO_BRAKE THEN
    CASE stMotionStage.Axis.Status.MotionState OF
        MC_AXISSTATE_UNDEFINED,
        MC_AXISSTATE_DISABLED,
        MC_AXISSTATE_ERRORSTOP:
            stMotionStage.bBrakeRelease := FALSE;
        MC_AXISSTATE_STANDSTILL:
            IF stMotionStage.nBrakeMode = E_StageBrakeMode.IF_MOVING THEN
                stMotionStage.bBrakeRelease := FALSE;
            ELSE
                stMotionStage.bBrakeRelease := TRUE;
            END_IF
        ELSE
            stMotionStage.bBrakeRelease := TRUE;
    END_CASE
END_IF

// Sync the epics status struct
stMotionStage.stAxisStatus := fbDriveVirtual.stAxisStatus;
stMotionStage.stAxisStatus.bEnabled := stMotionStage.bEnableDone;

// Override homing status, dmov as appropriate
stMotionStage.bHomed := fbMotionHome.bDone AND NOT fbMotionHome.bError;
stMotionStage.stAxisStatus.bHomed := stMotionStage.bHomed;
stMotionStage.stAxisStatus.bExecute := bExecute;
stMotionStage.stAxisStatus.nCommand := 3; // If this is not 3, the IOC stops updating positions during homing

// Fill in auxiliary status info
stMotionStage.fPosDiff := stMotionStage.Axis.NcToPlc.PosDiff;

// Reset everything when bReset is flagged
IF stMotionStage.bReset THEN
    stMotionStage.bEnable := FALSE;
    stMotionStage.bReset := FALSE;
    stMotionStage.bExecute := FALSE;
    stMotionStage.bError := FALSE;
    stMotionStage.nErrorId := 0;
    stMotionStage.sErrorMessage := '';
    stMotionStage.sCustomErrorMessage := '';
    bExecute := FALSE;
END_IF

fbEncoderValue(stMotionStage:=stMotionStage);
fbNCParams(
    stMotionStage:=stMotionStage,
    bEnable:=TRUE,
    tRefreshDelay:=T#1s);

// Save and restore as long as not an absolute encoder
fbSaveRestore(
    stMotionStage:=stMotionStage,
    bEnable:=stMotionStage.nHomingMode <> E_EpicsHomeCmd.NONE);]]></ST>
    </Implementation>
    <LineIds Name="FB_MotionStage">
      <LineId Id="3" Count="13" />
      <LineId Id="303" Count="0" />
      <LineId Id="17" Count="9" />
      <LineId Id="304" Count="0" />
      <LineId Id="27" Count="74" />
      <LineId Id="103" Count="1" />
      <LineId Id="381" Count="0" />
      <LineId Id="105" Count="0" />
      <LineId Id="389" Count="0" />
      <LineId Id="384" Count="3" />
      <LineId Id="364" Count="0" />
      <LineId Id="388" Count="0" />
      <LineId Id="107" Count="17" />
      <LineId Id="374" Count="0" />
      <LineId Id="126" Count="12" />
      <LineId Id="376" Count="3" />
      <LineId Id="375" Count="0" />
      <LineId Id="380" Count="0" />
      <LineId Id="139" Count="128" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>